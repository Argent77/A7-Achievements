BACKUP ~weidu_external/backup/A7-Achievements~
SUPPORT ~https://www.gibberlings3.net/forums/topic/33035-achievements-for-everyone/~
VERSION ~1.0~

ALWAYS
  // required because of non-standard BACKUP path
  OUTER_TEXT_SPRINT MOD_FOLDER ~A7-Achievements~
  // root folder for mutable data
  OUTER_TEXT_SPRINT WORK_FOLDER ~weidu_external~

  ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN
    FAIL @101 // DLC Merger is required before mods can be installed on this game. Check the readme for more information and a link to download DLC Merger.
  END

  INCLUDE ~%MOD_FOLDER%/lib/functions.tph~

  // Main install routine for the achievement system
  DEFINE_ACTION_FUNCTION INSTALL_MAIN
  INT_VAR show_compact = 1
  BEGIN
    // adding BGEE and SoD achievements in EET installation
    ACTION_IF (GAME_IS ~eet~) BEGIN
      EXTEND_BOTTOM ~achievem.bcs~ ~%MOD_FOLDER%/scripts/achievem-eet.baf~
    END

    LAF GET_GAME_LIST INT_VAR load_tra = 1 RET games END
    LAF INSTALL_ACHIEVEMENTS INT_VAR show_compact STR_VAR games END
  END
END


README ~A7-Achievements/readme/readme-%LANGUAGE%.txt~
       ~A7-Achievements/readme/readme.txt~

LANGUAGE ~English~
         ~english~
         ~A7-Achievements/languages/english/setup.tra~
         ~A7-Achievements/languages/english/common.tra~


BEGIN @2  // Compact summary
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet iwdee pstee~ &&
                   FILE_EXISTS_IN_GAME ~ACHIEVEM.2DA~ &&
                   VALID_SCRIPT_ACTIONS ~ChangeAIScript("",0)~) @100 // Enhanced Edition game required.
DESIGNATED 0
SUBCOMPONENT @1 // Steam Achievements as Journal Entries
LABEL ~A7-ACHIEVEMENTS-COMPACT~

LAF INSTALL_MAIN INT_VAR show_compact = 1 END


BEGIN @3  // Detailed summary
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet iwdee pstee~ &&
                   FILE_EXISTS_IN_GAME ~ACHIEVEM.2DA~ &&
                   VALID_SCRIPT_ACTIONS ~ChangeAIScript("",0)~) @100 // Enhanced Edition game required.
DESIGNATED 1
SUBCOMPONENT @1 // Steam Achievements as Journal Entries
LABEL ~A7-ACHIEVEMENTS-DETAILED~

LAF INSTALL_MAIN INT_VAR show_compact = 0 END


BEGIN @10 // Achievements reward
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~A7-Achievements.tp2~ 0) || 
                  (MOD_IS_INSTALLED ~A7-Achievements.tp2~ 1) @102 // Requires the main component of the mod to be installed.
DESIGNATED 10
LABEL ~A7-ACHIEVEMENTS-REWARDS~

LOAD_TRA ~%MOD_FOLDER%/languages/%LANGUAGE%/dialogs.tra~
LAF GET_GAME_LIST INT_VAR load_tra = 1 RET games END
LAF INSTALL_ITEM_REWARDS STR_VAR games END
